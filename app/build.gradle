plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.github.triplet.play'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        def commitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())
        def last_tag_commit = "git rev-list --tags --max-count=1".execute([], project.rootDir).text.trim()
        def last_tag = "git describe --tags ${last_tag_commit}".execute([], project.rootDir).text.trim().replace("_", " ")
        applicationId "dev.keader.correiostracker"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode commitCount
        versionName last_tag

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString()
                ]
            }
        }
    }

    signingConfigs {
        release {
            def password = System.getenv("APP_KEYSTORE_PASSWORD")
            def alias = System.getenv("APP_KEYSTORE_ALIAS")
            def signFile = rootProject.file("keadertracker.jks")

            storeFile signFile
            storePassword password
            keyAlias alias
            keyPassword password
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsEnabled: true]
        }

        debug {
            applicationIdSuffix '.debug'
            manifestPlaceholders = [crashlyticsEnabled: false]
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

play {
    track.set("production")
    serviceAccountCredentials.set(file("publish_key.json"))
}

dependencies {
    implementation project(path: ':CorreiosAPI')
    def room_version = '2.3.0'
    def nav_version = '2.3.5'
    def work_version = "2.5.0"
    def camerax_version = "1.1.0-alpha04"
    def camerax_ext_version = "1.0.0-alpha24"
    def tapTargetView_version ="1.13.2"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Lottie Animation
    implementation 'com.airbnb.android:lottie:3.7.0'

    // Room Database
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Timber Logs
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Life Cycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.35.1'
    kapt 'com.google.dagger:hilt-android-compiler:2.35.1'
    // Hilt-Worker
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // Worker
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:26.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'

    // Tap Target View
    implementation "com.getkeepsafe.taptargetview:taptargetview:$tapTargetView_version"

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.1.1'

    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_ext_version}"
    implementation "androidx.camera:camera-extensions:${camerax_ext_version}"

    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:16.2.0'
    implementation 'com.google.android.gms:play-services-mlkit-barcode-scanning:16.1.5'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

task buildNotes {
    doFirst {
        try {
            def branch = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()
            if (branch == "main") {
                def lastTag = "git describe --abbrev=0".execute([], project.rootDir).text.trim()
                def tagMessage = "git tag -l --format=%(contents) ${lastTag}".execute([], project.rootDir).text.trim()
                def production = new File(project.rootDir, "app/src/main/play/release-notes/pt-BR/production.txt")
                production.createNewFile()
                production.text = tagMessage
            } else if (branch == "development") {
                def message = "git log -1 --pretty=%B".execute([], project.rootDir).text.trim().split(":").reverse()[0]
                def internal = new File(project.rootDir, "app/src/main/play/release-notes/pt-BR/internal.txt")
                internal.createNewFile()
                internal.text = message
            }
        } catch (Exception ignored) {
            System.err.println("No git installed on the machine or not on a git repo. CorreiosTracker will not generate release notes")
        }
    }
}

afterEvaluate {
    def publish = tasks.findByName("publishReleaseBundle")
    if (publish != null) {
        publish.configure {
            dependsOn("buildNotes")
        }
    }
}

